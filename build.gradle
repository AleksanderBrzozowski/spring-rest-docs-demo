buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
        spockVersion = '1.1-groovy-2.4'
        vavrVersion = '0.9.2'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'
apply plugin: 'org.asciidoctor.convert'


group = 'com.brzozowski'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')

    // vavr
    compile("io.vavr:vavr:${vavrVersion}") {
        exclude group: 'io.vavr', module: 'vavr-match'
    }
    compile("io.vavr:vavr-jackson:${vavrVersion}") {
        exclude group: 'io.vavr', module: 'vavr-match'
    }
    compileOnly "io.vavr:vavr-match:${vavrVersion}"
    annotationProcessor "io.vavr:vavr-match:${vavrVersion}"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompile('org.springframework.boot:spring-boot-starter-test')

    testCompile 'io.rest-assured:rest-assured'
    testCompile('org.springframework.restdocs:spring-restdocs-restassured')
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile "org.spockframework:spock-spring:${spockVersion}"
}
ext {
    snippetsDir = file('build/generated-snippets')
    serveDocs = project.hasProperty('serveDocs')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    if (serveDocs) {
        dependsOn asciidoctor
        from("${asciidoctor.outputDir}/html5") {
            into 'static/docs'
        }
    }
    launchScript()
}

